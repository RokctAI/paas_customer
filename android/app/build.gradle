plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id 'com.google.gms.google-services'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties ()
def keystorePropertiesFile = rootProject.file ("key.properties")
if (keystorePropertiesFile.exists()) {keystoreProperties.load(new FileInputStream(keystorePropertiesFile))}
def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0.0'
}
def dartDefines = [:]

// 1. Load from .env/production.env in repo root (useful for CI and local fallbacks)
def envFile = rootProject.file("../.env/production.env")
if (envFile.exists()) {
    envFile.eachLine { line ->
        def trimmed = line.trim()
        if (trimmed && !trimmed.startsWith('#')) {
            def parts = trimmed.split('=', 2)
            if (parts.length == 2) {
                dartDefines[parts[0].trim()] = parts[1].trim()
            }
        }
    }
}

// 2. Load from flutter.dart-defines (CLI / IDE injection) - Highest priority
if (project.hasProperty('flutter.dart-defines')) {
    dartDefines = dartDefines + project.property('flutter.dart-defines')
        .split(',')
        .collectEntries { entry ->
            def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
            [(pair[0]): pair.length > 1 ? pair[1] : '']
        }
}

android {
    namespace dartDefines.CUSTOMER_ANDROID_PACKAGE_NAME
    compileSdkVersion 34
    ndkVersion "26.1.10909125"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += [
                '-Xskip-metadata-version-check',
                '-opt-in=kotlin.RequiresOptIn'
        ]
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId dartDefines.CUSTOMER_ANDROID_PACKAGE_NAME
        minSdkVersion 23
        multiDexEnabled true
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        // Synchronized configurations from dart-defines
        resValue "string", "google_maps_api_key", dartDefines.GOOGLE_MAPS_API_KEY ?: ""
        resValue "string", "facebook_app_id", dartDefines.FACEBOOK_APP_ID ?: ""
        resValue "string", "facebook_client_token", dartDefines.FACEBOOK_CLIENT_TOKEN ?: ""
        resValue "string", "deep_link_host", dartDefines.DEEP_LINK_URL ?: ""
        resValue "string", "app_name", dartDefines.CUSTOMER_APP_NAME ?: ""
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile keystoreProperties["storeFile"] ?file(keystoreProperties["storeFile"]) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Add debug symbols
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    // Common dependencies for all build types
    implementation 'androidx.multidex:multidex:2.0.1'

    // Release-only dependencies
    releaseImplementation platform('com.google.firebase:firebase-bom:32.7.2')
    releaseImplementation 'com.google.firebase:firebase-analytics-ktx'
}